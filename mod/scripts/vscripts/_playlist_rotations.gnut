global function PlaylistRotations_Init
global function InitPlaylistRotation
global function SetPlaylistRotations
global function ClearNextPlaylistRotations
global function SetPlaylistRotationTime
global function SetNextPlaylistByName

struct {
    array<PlaylistRotationDefinition> store
    array<PlaylistRotationDefinition> next
    PlaylistRotationDefinition& current 
    int rotationTime = 60 * 60
} file

void function PlaylistRotations_Init()
{
    try {
        #if SERIALIZEDIO
            file.next = BPTypePun( BPLoadFile( "playlist_rotations" ) )
        #else
            throw "no SERIALIZEDIO"
        #endif
    } catch ( err ) {
        print( "PlaylistRotations_Init " + err )
    }
    
    if ( IsLobby() )
    {
        if ( file.store.len() == 0 )
        {
            print("no playlists")
            return
        }

        PlaylistRotationDefinition lastCurrent
        try {
            #if SERIALIZEDIO
                lastCurrent = BPTypePun( BPLoadFile( "playlist_current" ) )[0]
            #endif
        } catch ( err ) {
            print( "PlaylistRotations_Init " + err )
        }

        if ( CheckPlaylistSwap() )
        {
            print("swaping playlists " + file.store.len() + " " + file.next.len())
            if ( file.next.len() == 0 )
            {
                print("refreshing playlists")
                file.next = clone file.store
            }
                
            print("playlists state " + file.store.len() + " " + file.next.len())
            file.next.randomize()
            print("playlists state " + file.store.len() + " " + file.next.len())
            file.current = clone file.next.pop()
            print("playlists state " + file.store.len() + " " + file.next.len())

            #if SERIALIZEDIO
                BPSaveFile( "playlist_rotations", file.next )
            #endif
        } else {
            file.current = lastCurrent
        }

        #if SERIALIZEDIO
            BPSaveFile( "playlist_current", [ file.current ] ) // saving as an array because the type punning function can only output arrays of the struct
        #endif

    	ServerCommand( "setplaylist private_match" )

        foreach ( string convar, string val in file.current.convars )
            SetConVarString( convar, val )

        thread WaitForPlayers()
    }
    else
    {
        thread WaitForGameStart()
    }
}

void function WaitForGameStart()
{
    while ( GetGameState() != eGameState.Playing )
        wait WaitFrame()

    if ( file.current.timeOverwrite != null ) 
        SetGameEndTime( expect float( file.current.timeOverwrite ) )
}

void function WaitForPlayers()
{
    while ( GetPlayerArray().len() == 0 )
        wait WaitFrame()

    string playlist = file.current.playlist == "" ? "tdm" : file.current.playlist
    string map = file.current.map == "" ? file.current.playlist == "" ? "mp_forwardbase_kodai" : RandomPlaylistMap( file.current.playlist ) : file.current.map

	ServerCommand( "setplaylist " + playlist )
    SetConVarString( "mp_gamemode", file.current.playlist )
    PrivateLobby_SetMap( map )
    PrivateLobby_SetMode( playlist )
    thread SetupPrivateMatchUIVarsWhenReady()
    print( "set playlist rotations to " + file.current.name )

    wait 1

    thread SetupPrivateMatchUIVarsWhenReady()
    PrivateLobby_StartMatch()
}

void function SetPlaylistRotations( array<PlaylistRotationDefinition> rotations )
{
    file.store = clone rotations
}

void function SetPlaylistRotationTime( int t )
{
    file.rotationTime = t
}

void function ClearNextPlaylistRotations()
{
    file.next = []
}

PlaylistRotationDefinition function InitPlaylistRotation( string name, int ty, string playlistName, string map = "" )
{
    PlaylistRotationDefinition playlist
    playlist.name = name
    playlist.ty = ty
    playlist.playlist = playlistName
    playlist.map = map

    return playlist
}

string function RandomPlaylistMap( string playlist )
{
    array<string> maps = GetPlaylistMaps( playlist ) 
    maps.randomize()
    return maps.pop()
}

bool function CheckPlaylistSwap()
{
    int nextplaylisttime = GetUnixTimestamp()

    try {
        #if SERIALIZEDIO
            nextplaylisttime = expect int( BPLoadFile( "playlist_time" ) )
        #endif
    } catch ( err ) {
        print( "CheckPlaylistSwap " + err )
    }

    // int fails? so just check for float too
    try {
        #if SERIALIZEDIO
            nextplaylisttime = ( expect float( BPLoadFile( "playlist_time" ) ) ).tointeger()
        #endif
    } catch ( err2 ) {
        print( "CheckPlaylistSwap " + err2 )
    }

    if ( nextplaylisttime <= GetUnixTimestamp() )
    {
        try {
            #if SERIALIZEDIO
                BPSaveFile( "playlist_time", GetUnixTimestamp() + file.rotationTime )
            #endif
        } catch ( err ) {
            print( "CheckPlaylistSwap " + err )
        }
        return true
    }

    return false
}

void function SetNextPlaylistByName( string name )
{
    PlaylistRotationDefinition playlist
    foreach ( PlaylistRotationDefinition pl in file.store )
    {
        if ( pl.name == name )
        {
            playlist = pl
        }
    }

    if ( playlist.name == "" )
    {
        print( "playlist " + name + " does not exist" )
        return
    }

    #if SERIALIZEDIO
        BPSaveFile( "playlist_current", [ playlist ] ) // saving as an array because the type punning function can only output arrays of the struct
        BPSaveFile( "playlist_time", GetUnixTimestamp() + file.rotationTime )
    #endif
}
